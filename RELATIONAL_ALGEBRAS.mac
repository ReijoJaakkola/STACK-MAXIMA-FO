/*
Code related to relational algebra.

@Reijo Jaakkola
@reijo.jaakkola@tuni.fi
*/

/*
Syntax of relational algebras is generated by the following grammar.

T ::= R | J[T,...,T] | I[T,...,T] | U[T,...,T] | C[T,i,j] | S[T,i_1,...,i_n]

where R is a relational symbol. Vocabularies are managed by dictionaries consisting of
pairs ["R",i], where i is the arity of R.

Relations are pairs, where the first element is an integer describing the arity of the relation,
and the second element is a set of arrays. An interpretation is a dictionary mapping
relational symbols to relations.
*/

/*
Helper function for checking that a given string is 
representing a number.

@param[in]  C   The string.
@return         True iff C is a string representing an integer.
*/
INTEGERP(C):= block([X],
    /*First check that C is a string.*/
    if (stringp(C) = false) then (return(false)),
    X:charlist(C),
    /*We will analyse the ASCII codes for each character in C.*/
    X:map(cint,X),
    /*Check that the ASCII code for every character in C is in the interval [48,57].*/
    X:map(lambda([x], 47 < x and x < 58),X),
    if(member(false,X)) then 
        (return(false))
    else return(true))$

/*
Type conversion from string to int.
NOTE: Assumes that the input is a string representing a number.

@param[in]	C   The string.
@return         The integer that C represents.
*/
CONVINT(C):=block([X, INT],
    INT:0, 
	X:charlist(C),
    /*We will analyse the ASCII codes for each character in C.*/
    X:map(cint,X),
    /*If c is a character representing a number, then the number is*/
    /*is the ASCII code of c minus 48.*/
    X:map(lambda([x], x - 48),X),
    /*Convert a list of digits into a number.*/
    for i:1 thru length(X) do(
        INT: INT + X[i]*10^(length(X)-i)),
    return(INT))$
	
/*
@param[in]	relationalSymbol
@param[in]	vocabulary
@return		Arity of the relational symbol if the symbol belonged to the vocabulary,
			and false otherwise.
*/
arity(relationalSymbol, vocabulary) := block([], return(assoc(relationalSymbol, vocabulary)))$

/*
@param[in]	relation1
@param[in]	relation2
@return     The cartesian product of the two relations.
*/
cartesianProduct(relation1, relation2) := block([product],
	/*Make a list out of the cartesian product so that we can iterate it.*/
	product: listify(cartesian_product(relation1[2], relation2[2])),
	for i:1 thru length(product) do (
		/*Needed to convert f.e. [[0,1],0] to [0,1,0].*/
		product[i]: flatten(product[i])),
	/*Combine the result with the new arity.*/
	return([relation1[1] + relation2[1],setify(product)]))$

/*
@param[in]	relation
@param[in]	index1
@param[in]	index2
@return		False if the given indices were not valid.
            Otherwise the set of those tuples in the relation where the tuples agree on
            the elements in the places pointed to by the indexes.
*/
choose(relation, index1, index2) := block([listifiedRelation, output],
	/*Check the indices.*/
	if (relation[1] < index1 or relation[1] < index2) then (
		return(false)),
	/*Listify the relation so that we can iterate it.*/
	listifiedRelation: listify(relation[2]),
	output: makelist(0,0),
	for i:1 thru length(listifiedRelation) do (
		/*Add only those tuples in which the tuples indexed by index1 and index2 are equal.*/
		if (listifiedRelation[i][index1] = listifiedRelation[i][index2]) then (
			output: push(listifiedRelation[i], output))),
	/*Combine the result with the original arity.*/
	return([relation[1], setify(output)]))$
	
/*
@param[in]	relation
@param[in]  indexes		An array of indices.
@return     False if the given indices were not valid. Otherwise returns
            a relation which consists of tuples for which the elements were
			selected according to the indices specified in the array indexes.
			The arity of the output relation will be the length of the indexes array.
			Note that indexes can contain multiple times the same index.
*/
select(relation, indexes) := block([listifiedRelation, output, tuple],
	/*Check that all of the given indices are valid.*/
	for i:1 thru length(indexes) do (
		if (relation[1] < indexes[i]) then (
			return(false))),
	/*Listify the relation so that we can iterate it.*/
	listifiedRelation: listify(relation[2]),
	output: makelist(0,0),
	for i:1 thru length(listifiedRelation) do (
		tuple: makelist(0,0),
		for j:1 thru length(indexes) do (
			tuple: append([listifiedRelation[i][j]],tuple)),
		output: push(tuple, output)),
	/*Combine the result with the new arity.*/
	return([length(indexes), setify(output)]))$